plugins {
    id 'application'
    id 'java'
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

// current version of the Engine:
ext.jmonkeyengineVersion = '3.5.0-stable'

tasks.withType(JavaCompile) { // Java compile-time options:
    options.compilerArgs << '-Xdiags:verbose'
    options.compilerArgs << '-Xlint:unchecked'
    //options.deprecation = true // to provide detailed deprecation warnings
    options.encoding = 'UTF-8'
    if (JavaVersion.current().isCompatibleWith(JavaVersion.VERSION_1_10)) {
        options.release = 8
    }
}

tasks.withType(JavaExec) { // runtime options:
    args = ['../RyzomAssets']
    //args = ['../RyzomAssets', 'noanimations'] // models only
    //args = ['../RyzomAssets', 'nomodels'] // animations only
    classpath sourceSets.main.runtimeClasspath
    enableAssertions true
    //jvmArgs '-verbose:gc'
    jvmArgs '-Xms4g', '-Xmx4g' // enlarge the Java heap
}

mainClassName = 'net.bithaven.jme.ryzom.RyzomConverter'
if (!hasProperty('mainClass')) {
    ext.mainClass = mainClassName
}
jar.manifest.attributes('Main-Class': mainClassName)

repositories {
    //mavenLocal() // to find local SNAPSHOTs of libraries
    mavenCentral()
}

dependencies {
    implementation 'org.apache.directory.studio:org.apache.commons.io:2.4'
    compileOnly 'org.realityforge.javax.annotation:javax.annotation:1.0.1'

    implementation 'org.jmonkeyengine:jme3-core:' + jmonkeyengineVersion
    runtimeOnly 'org.jmonkeyengine:jme3-desktop:' + jmonkeyengineVersion

    // RyzomConverter doesn't use jme3-jogg nor jme3-plugins
    //  -- they are included solely to avoid warnings from AssetConfig.
    runtimeOnly 'org.jmonkeyengine:jme3-jogg:' + jmonkeyengineVersion
    runtimeOnly 'org.jmonkeyengine:jme3-plugins:' + jmonkeyengineVersion
}

// cleanup tasks:

clean.dependsOn('cleanWrittenAssets')

task cleanWrittenAssets(type: Delete) { // output directory
    delete 'assets/ryzom-assets/export'
}
